# Copyright 2025 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

locals {
  gcs_lifecycle_age              = 30
  bq_connector_refresh_interval  = "86400s" # Daily
  wait_for_bq_datastore_duration = "120s"
}

# ---------------------------------------------------------------------------- #
#  Discovery Engine CMEK Config                                              #
# ---------------------------------------------------------------------------- #

# CMEK Configuration for Discovery Engine (Conditional)
resource "google_discovery_engine_cmek_config" "default" {
  count = var.create_data_stores ? 1 : 0

  project        = var.main_project_id
  location       = var.geolocation # should be "US"
  cmek_config_id = "default_cmek_config"
  kms_key        = local.cmek_key_id
  set_default    = true
  provider       = google-beta

  depends_on = [
    google_kms_crypto_key_iam_member.discoveryengine_sa_kms_access,
    google_kms_crypto_key_iam_member.gcs_sa_kms_access,
    google_project_service.services,
    time_sleep.wait_for_services,
  ]
}

# ---------------------------------------------------------------------------- #
#  Google Cloud Storage Data Stores                                            #
# ---------------------------------------------------------------------------- #

# GCS Buckets for Discovery Engine Data Sources
resource "google_storage_bucket" "gemini_enterprise_gcs_bucket" {
  for_each = var.create_data_stores ? toset(var.gcs_data_store_names) : []

  project                     = var.main_project_id
  name                        = "${var.main_project_id}-${each.key}-data"
  location                    = var.geolocation
  uniform_bucket_level_access = true
  force_destroy               = false # Set to true only for non-production

  encryption {
    default_kms_key_name = local.cmek_key_id
  }

  lifecycle_rule {
    condition {
      age = local.gcs_lifecycle_age # Example: delete objects older than 30 days
    }
    action {
      type = "Delete"
    }
  }

  labels = {
    environment = var.environment
    service     = "${var.prefix}-gcs"
    data_store  = each.key
  }

  depends_on = [
    google_kms_crypto_key_iam_member.gcs_sa_kms_access
  ]
}

# Random suffix for GCS Data Store IDs
resource "random_string" "gcs_suffix" {
  for_each = var.create_data_stores ? toset(var.gcs_data_store_names) : []

  length  = 6
  special = false
  upper   = false
}

# Discovery Engine Data Stores for GCS
resource "google_discovery_engine_data_store" "gemini_enterprise_gcs_data_store" {
  for_each = var.create_data_stores ? toset(var.gcs_data_store_names) : []

  project           = var.main_project_id
  location          = var.geolocation # Must match the Data Store and Engine location
  data_store_id     = "${each.key}-gcs-data-store-${random_string.gcs_suffix[each.key].result}"
  display_name      = each.key
  industry_vertical = "GENERIC"
  content_config    = "CONTENT_REQUIRED"
  solution_types    = ["SOLUTION_TYPE_SEARCH"]
  kms_key_name      = local.cmek_key_id
  provider          = google-beta

  document_processing_config {
    default_parsing_config {
      digital_parsing_config {}
    }
  }

  depends_on = [
    google_discovery_engine_cmek_config.default,
    google_kms_crypto_key_iam_member.discoveryengine_sa_kms_access,
    google_kms_crypto_key_iam_member.gcs_sa_kms_access,
    google_project_service.services,
    time_sleep.wait_for_services,
  ]
}

# ---------------------------------------------------------------------------- #
#  BigQuery Data Stores                                                        #
# ---------------------------------------------------------------------------- #

locals {
  bq_configs = { for idx, config in var.bq_data_store_configs : idx => config }
}

resource "google_bigquery_dataset" "gemini_enterprise_bq_dataset" {
  for_each = var.create_data_stores ? local.bq_configs : {}

  project       = var.main_project_id
  dataset_id    = each.value.dataset_id
  friendly_name = "Gemini Enterprise Data - ${each.value.dataset_id}"
  description   = "Dataset for Discovery Engine connector - ${each.value.dataset_id}"
  location      = var.geolocation # Or a more specific region specific location if desired

  default_encryption_configuration {
    kms_key_name = local.cmek_key_id
  }

  depends_on = [
    google_project_service.services,
    time_sleep.wait_for_services,
    google_kms_crypto_key_iam_member.bq_sa_kms_access
  ]
}

resource "google_bigquery_table" "gemini_enterprise_bq_table" {
  for_each = var.create_data_stores ? local.bq_configs : {}

  project             = var.main_project_id
  dataset_id          = google_bigquery_dataset.gemini_enterprise_bq_dataset[each.key].dataset_id
  table_id            = each.value.table_id
  deletion_protection = false

  # Define a default schema, users can adapt this as needed
  schema = <<EOF
[
  {
    "name": "id",
    "type": "STRING",
    "mode": "REQUIRED",
    "description": "The ID of the document"
  },
  {
    "name": "jsonData",
    "type": "JSON",
    "mode": "NULLABLE",
    "description": "The JSON content of the document"
  },
  {
    "name": "content",
    "type": "STRING",
    "mode": "NULLABLE",
    "description": "The text content of the document"
  }
]
EOF

  depends_on = [
    google_bigquery_dataset.gemini_enterprise_bq_dataset,
    google_kms_crypto_key_iam_member.bq_sa_kms_access
  ]
}

# Random suffix for BQ Data Store IDs
resource "random_string" "bq_suffix" {
  for_each = var.create_data_stores ? local.bq_configs : {}

  length  = 6
  special = false
  upper   = false
}

# ---------------------------------------------------------------------------- #
#  Dynamic Discovery Engine with BigQuery Connectors                           #
# ---------------------------------------------------------------------------- #
resource "google_discovery_engine_data_store" "gemini_enterprise_bq_data_store" {
  for_each = var.create_data_stores ? local.bq_configs : {}

  project                      = var.main_project_id
  location                     = var.geolocation # Must match the Data Store and Engine location
  data_store_id                = "${replace(each.value.dataset_id, "_", "-")}-bq-data-store-${random_string.bq_suffix[each.key].result}"
  display_name                 = "${each.value.dataset_id} - ${each.value.table_id}"
  industry_vertical            = "GENERIC"
  content_config               = "CONTENT_REQUIRED"
  solution_types               = ["SOLUTION_TYPE_SEARCH"]
  kms_key_name                 = local.cmek_key_id
  skip_default_schema_creation = true
  provider                     = google-beta

  depends_on = [
    google_discovery_engine_cmek_config.default,
    google_kms_crypto_key_iam_member.discoveryengine_sa_kms_access,
    google_kms_crypto_key_iam_member.gcs_sa_kms_access,
    google_kms_crypto_key_iam_member.bq_sa_kms_access,
    google_project_service.services,
    time_sleep.wait_for_services,
  ]
}

# Add a delay to allow the DataStore to be created by the connector
resource "time_sleep" "wait_for_bq_datastore" {
  for_each        = var.create_data_stores ? local.bq_configs : {}
  create_duration = "30s"
  depends_on      = [google_discovery_engine_data_store.gemini_enterprise_bq_data_store]
}

# ---------------------------------------------------------------------------- #
#  Common ACL Config                                                           #
# ---------------------------------------------------------------------------- #

# Discovery Engine ACL Config First-party
resource "google_discovery_engine_acl_config" "gemini_enterprise_acl_config" {
  project  = var.main_project_id
  location = var.geolocation # Must match the connector location
  idp_config {
    idp_type = var.acl_idp_type
    dynamic "external_idp_config" {
      for_each = var.acl_idp_type == "THIRD_PARTY" ? [1] : []
      content {
        workforce_pool_name = var.acl_workforce_pool_name
      }
    }
  }
  provider = google-beta

  depends_on = [
    time_sleep.wait_for_services
  ]
}
